plugins {
    id 'java'
    id 'maven-publish'
    id 'com.palantir.git-version' version '0.12.2'
}

group 'info.journeymap'

if (project.hasProperty("production")) {
    version release
} else {
    def details = versionDetails()
    version "${release}-${details.gitHash}"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task webpack(type: Exec) {
    inputs.file("package-lock.json")
    inputs.file("webpack.config.js")
    inputs.dir("$projectDir/src/main/js")
    inputs.dir("$projectDir/src/main/css")
    inputs.dir("$projectDir/src/main/images")
    inputs.dir("$projectDir/src/main/resources")

    outputs.dir("$projectDir/src/main/resources/assets/journeymap/web/bundled")

    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        commandLine "cmd", "/c", "npm", "run", "build"
    } else {
        commandLine "npm", "run", "build"
    }
}

tasks.processResources.dependsOn webpack

clean.doFirst {
    // Remove the Webpack bundled assets as part of the clean task
    delete "$projectDir/src/main/resources/assets/journeymap/web/bundled"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/TeamJM/webmap-client")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
